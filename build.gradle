/* (c) Michal Nov√°k, libeetlite, it.novakmi@gmail.com, see LICENSE file */

apply plugin: 'groovy'
apply plugin: 'maven' // to use maven repository
apply plugin: 'idea' // to generate project+module files for Intellij Idea

def groupId = 'com.github.novakmi'
version '0.4.1'
sourceCompatibility = '1.6'
def installDir = file('./install')

apply plugin: 'com.github.ben-manes.versions' // gradle dependencyUpdates -Drevision=release

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.15.0'
    }
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile localGroovy()
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    testCompile group: 'org.testng', name: 'testng', version: '6.12'
    testCompile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testCompile group: 'org.codehaus.janino', name: 'janino', version: '3.0.7'
    testCompile group: 'com.github.groovy-wslite', name: 'groovy-wslite', version: '1.1.3'
}


jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': version,
                'Specification-Vendor': 'Michal Novak (it.novakmi@gmail.com)',
                'Built-By': 'it.novakmi',
                'Specification-Title': project.name,
                'Extension-Name': project.name,
                'Specification-Version': version
        )
    }
}

clean.doLast {  //add to clean task
    installDir.deleteDir()
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from "${project.projectDir}/src/main"
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    classifier = 'groovydoc'
    from groovydoc.destinationDir
}


test {
    systemProperties["testDataDir"] = "${projectDir}/src/test/testData"
    useTestNG() {
        suiteXmlBuilder().suite(name: 'Test libeetlite') {
            test(name: 'libeetlite'/*, annotations: 'JDK', verbose: '1'*/) {
                groups {
                    run {
                        include(name: "basic")
                        exclude(name: "internet")
                    }
                }
                classes([:]) {
                    'class'(name: "com.github.novakmi.libeetlite.test.EetliteUtilTest")
                    'class'(name: "com.github.novakmi.libeetlite.test.EetliteXmlTest")
                }
            }
        }
    }
    //options {
    //        listeners << 'org.uncommons.reportng.HTMLReporter'
    //        listeners << 'org.uncommons.reportng.JUnitXMLReporter'
    //    }
}

task prepareInstall(type: Copy, dependsOn: [jar, sourcesJar, groovydocJar]) {
    installDir.mkdirs()
    //logger.quiet("externalJars {}", externalJars)
    from configurations['compile']?.files/*?.findAll { it.absolutePath.indexOf('unspecified') < 0 }*/
    from "${project.projectDir}/build/libs"
    into installDir
}

// zip into build/distributions
task zipInstall(type: Zip, dependsOn: prepareInstall) {
    from installDir.toString()
}

artifacts { // upload also sources and groovydoc
    archives sourcesJar
    archives groovydocJar
}

uploadArchives {  // support to upload to github emulated maven repository
    def localMavenDir = System.getenv()['LOCAL_MAVEN_DIR']
    if (!localMavenDir) {
        logger.error("LOCAL_MAVEN_DIR is not defined! Artifacts will not be exported!")
    } else {
        repositories {
            mavenDeployer {
                repository(url: "file://localhost/${localMavenDir}/releases")
                pom.version = version
                pom.groupId = groupId
            }
        }
    }
}
