/* (c) Michal Nov√°k, libeetlite, it.novakmi@gmail.com, see LICENSE file */

buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'base'
    id 'groovy'
    id 'idea'
    id 'com.github.ben-manes.versions' version '0.28.0' // gradle dependencyUpdates -Drevision=release
    id 'maven-publish' // to use maven repository
}

def installDir = file('./install')

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    withSourcesJar()
}

repositories {
    jcenter()
}

dependencies {
    implementation group: 'org.codehaus.groovy', name: 'groovy', version: "${project.groovyVersion}"
    implementation group: 'org.codehaus.groovy', name: 'groovy-xml', version: "${project.groovyVersion}"
    implementation group: 'org.codehaus.groovy', name: 'groovy-dateutil', version: "${project.groovyVersion}"
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.30'

    testImplementation group: 'org.testng', name: 'testng', version: '7.1.1'
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    testImplementation group: 'org.codehaus.janino', name: 'janino', version: '3.1.0'
    testImplementation group: 'com.github.groovy-wslite', name: 'groovy-wslite', version: '1.1.3'
}

jar {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version,
            'Specification-Vendor': project.author,
            'Built-By': project.author,
            'Specification-Title': project.name,
            'Extension-Name': project.name,
            'Specification-Version': project.version
        )
    }
}

clean.doLast {  //add to clean task
    installDir.deleteDir()
}

task groovydocJar(type: Jar, dependsOn: groovydoc) {
    archiveClassifier = 'groovydoc'
    from groovydoc.destinationDir
}

test {
    systemProperties["testDataDir"] = "${projectDir}/src/test/testData"
    useTestNG() {
        suiteXmlBuilder().suite(name: 'Test libeetlite') {
            test(name: 'libeetlite'/*, annotations: 'JDK', verbose: '1'*/) {
                groups {
                    run {
                        include(name: "basic")
                        exclude(name: "internet")
                    }
                }
                classes([:]) {
                    'class'(name: "com.github.novakmi.libeetlite.test.EetliteUtilTest")
                    'class'(name: "com.github.novakmi.libeetlite.test.EetliteXmlTest")
                }
            }
        }
    }
    //options {
    //        listeners << 'org.uncommons.reportng.HTMLReporter'
    //        listeners << 'org.uncommons.reportng.JUnitXMLReporter'
    //    }
}

task prepareInstall(type: Copy, dependsOn: [jar, sourcesJar, groovydocJar]) {
    installDir.mkdirs()
    //from configurations.compileClasspath.files // if we need also dependencies
    from "${project.projectDir}/build/libs"
    into installDir
}

// zip into build/distributions
task zipInstall(type: Zip, dependsOn: prepareInstall) {
    from installDir.toString()
}

publishing {   // use gradle publish
    def localMavenDir = System.getenv()['LOCAL_MAVEN_DIR']
    if (!localMavenDir) {
        logger.error("LOCAL_MAVEN_DIR is not defined! Artifacts will not be exported!")
    } else {
        publications {
            eetLiteLibrary(MavenPublication) {
                from components.java
                artifact groovydocJar {
                    classifier "groovydoc"
                }
            }
        }
        repositories {
            maven {
                name = 'novakmirepo'
                url = "file://${localMavenDir}/releases"
            }
        }
    }
}
